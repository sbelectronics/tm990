Name multi-addr;
Partno G22V10#2;
Revision;
Date 09/1/2023;
Designer Scott Baker;
Company ;
Location ;
Assembly TM990 multi board;
Device G22V10;

/* Inputs: */
Pin 1  = OPT4;
Pin 2  = DBIN;
Pin 3  = A3;
Pin 4  = A4;
Pin 5  = A5;
Pin 6  = A6;
Pin 7  = A7;
Pin 8  = A8;
Pin 9  = A9;
Pin 10 = A10;
Pin 11 = A11;
Pin 13 = A12;

/* Outputs:  */
Pin 23 = A13;
Pin 22 = A14;
Pin 21 = A15;
Pin 20 = !IOSEL;
Pin 19 = !VCS;
Pin 18 = !IDE_CS0;
Pin 17 = !MCS0;
Pin 16 = !MCS1;
Pin 15 = !MCS2;
Pin 14 = !MCS3;

FIELD ADDR = [A15..A3];

IDE_READ = (ADDR:E00X) & DBIN;
IDE_WRITE = (ADDR:E01X) & (!DBIN);

VDP_DATA_READ = (ADDR:E020) & DBIN;
VDP_DATA_WRITE = (ADDR:E030) & (!DBIN);
VDP_STAT_READ = (ADDR:E022) & DBIN;
VDP_REG_WRITE = (ADDR:E032) & (!DBIN);

MCS0_R = (ADDR:E0EX) & DBIN;
MCS0_W = (ADDR:E0FX) & (!DBIN);
MCS1_R = (ADDR:E0CX) & DBIN;
MCS1_W = (ADDR:E0DX) & (!DBIN);
MCS2_R = (ADDR:E0AX) & DBIN;
MCS2_W = (ADDR:E0BX) & (!DBIN);
MCS3_R = (ADDR:E08X) & DBIN;
MCS3_W = (ADDR:E09X) & (!DBIN);

MCS0 = MCS0_R # MCS0_W;
MCS1 = MCS1_R # MCS1_W;
MCS2 = MCS2_R # MCS2_W;
MCS3 = MCS3_R # MCS3_W;

MCS_CS = MCS0_R # MCS0_W  # MCS1_R # MCS1_W # MCS2_R # MCS2_W # MCS3_R # MCS3_W;

VDP_CS = (VDP_DATA_READ # VDP_DATA_WRITE # VDP_STAT_READ # VDP_REG_WRITE);

IDE_CS0 = (IDE_READ # IDE_WRITE);

VCS = VDP_CS;

IOSEL = MCS_CS # IDE_READ # IDE_WRITE # VDP_DATA_READ # VDP_DATA_WRITE # VDP_STAT_READ # VDP_REG_WRITE;

